// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
  apply from: 'jfrog-constants.gradle'
  ext.kotlin_version = '1.8.10'
  repositories {
    google()
    mavenCentral()
    maven {
      url project.ext.jfrogRepositoryUrl
      credentials {
        username = project.ext.jfrogUser
        password = project.ext.jfrogAdminToken
      }
    }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:8.1.0'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

    classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.29.2"

    // NOTE: Do not place your application dependencies here; they belong
    // in the individual module build.gradle files
  }
}

allprojects {
  apply plugin: "com.jfrog.artifactory"
  apply plugin: "maven-publish"
  repositories {
    mavenCentral()
    google()
    maven {
      url rootProject.ext.jfrogRepositoryUrl
      credentials {
        username = rootProject.ext.jfrogUser
        password = rootProject.ext.jfrogAdminToken
      }
    }
  }
}

artifactory {
  contextUrl = rootProject.ext.jfrogContextUrl
  publish {
    repository {
      repoKey = rootProject.ext.jfrogRepoKey
      username = rootProject.ext.jfrogUser
      password = rootProject.ext.jfrogAdminToken
      maven = true
    }
    defaults {
      publishArtifacts = true
      publishPom = true
    }
  }
}

project("dspoclibrary") {
  artifactoryPublish.dependsOn("assemble")

  publishing {
    publications {
      aar(MavenPublication) {
        artifactId = rootProject.ext.dspocLibraryArtifactId
        groupId = rootProject.ext.dspocLibraryGroupId
        version = rootProject.ext.dspocLibraryVersion
        artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
      }
    }
  }

  artifactoryPublish {
    publications(publishing.publications.aar)
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}
